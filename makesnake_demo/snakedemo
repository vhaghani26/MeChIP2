configfile: "demo.yaml"

rule all:
	input:
		#expand("peaks/{sample}_model.r", sample=config["samples"]),
		expand("bigwig_files/{sample}.bw", sample=config["samples"]),
		expand("peaks/{sample}_peaks.xls", sample=config["samples"]),
		expand("peaks/{sample}_summits.bed", sample=config["samples"]),
		expand("peaks/{sample}_treat_pileup.bdg", sample=config["samples"]),
		expand("peaks/{sample}_peaks.narrowPeak", sample=config["samples"]),
		expand("peaks/{sample}_control_lambda.bdg", sample=config["samples"])
		

rule align_reads:
	input:
		expand("aligned_reads/{sample}.sam", sample=config["samples"])

rule align_reads_wc:
	input:
		genome = multiext("data/genome", ".amb", ".ann", ".bwt", ".pac", ".sa"),
		r = "data/samples/{sample}.fastq"
	output:
		"aligned_reads/{sample}.sam"
	shell:
		"bwa mem data/genome {input.r} > {output}"

rule sam_to_bam:
	input:
		"aligned_reads/{sample}.sam"
	output:
		"bam_files/{sample}.bam"
	shell:
		"samtools view -Sb {input} > {output}"

rule sam_fixmate:
	input:
		"bam_files/{sample}.bam"
	output:
		"bam_files/{sample}.fixmate.bam"
	shell:
		"samtools fixmate -rcm -O bam {input} {output}"
		
rule sam_sort:
	input:
		"bam_files/{sample}.fixmate.bam"
	output:
		"bam_files/{sample}.sorted.fixmate.bam"
	shell:
		"samtools sort -O bam {input} > {output}"

rule markdup:
	input:
		"bam_files/{sample}.sorted.fixmate.bam"
	output:
		"bam_files/{sample}.sorted.dedup.bam"
	shell:
		"samtools markdup -r --mode s {input} {output}"


rule sam_index:
	input:
		"bam_files/{sample}.sorted.dedup.bam"
	output:
		"bam_files/{sample}.sorted.dedup.bam.bai"
	shell:
		"samtools index {input}"

rule bam_to_bigwig:
	input:
		index = "bam_files/{sample}.sorted.dedup.bam.bai",
		bam = "bam_files/{sample}.sorted.dedup.bam"
	output:
		"bigwig_files/{sample}.bw"
	shell:
		"bamCoverage -b {input.bam} -o {output}"

rule call_peaks:
	input:
		"bam_files/{sample}.sorted.dedup.bam"
	output:
		#"peaks/{sample}_model.r",
		"peaks/{sample}_peaks.xls",
		"peaks/{sample}_summits.bed",
		"peaks/{sample}_treat_pileup.bdg",
		"peaks/{sample}_peaks.narrowPeak",
		"peaks/{sample}_control_lambda.bdg"
	shell:
		"macs3 callpeak -t {input} -f BAM -n {wildcards.sample} --bdg --nomodel --outdir peaks/"

